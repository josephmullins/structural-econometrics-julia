{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# Automatic Differentiation\n",
        "\n",
        "Automatic differentiation (AD) is a technique for computing exact derivatives of functions specified by computer programs. Unlike symbolic differentiation (which manipulates mathematical expressions) or numerical differentiation (which uses finite differences), AD exploits the fact that every program, no matter how complex, executes a sequence of elementary operations. By applying the chain rule systematically to these operations, AD computes derivatives to machine precision.\n",
        "\n",
        "## Why AD Matters for Structural Estimation\n",
        "\n",
        "In structural econometrics, we frequently need gradients for:\n",
        "\n",
        "- Optimization (MLE, GMM, minimum distance)\n",
        "    - Gradient-free methods such as Nelder-Mead are popular, of course, but are less efficient\n",
        "- Computing standard errors\n",
        "- Solving models with equilibrium conditions (using Newton's method, for example)\n",
        "\n",
        "Hand-coding derivatives is tedious and error-prone. Finite differences are slow (requiring $O(n)$ function evaluations for an $n$-dimensional gradient) and can be numerically unstable. AD provides exact gradients efficiently.\n",
        "\n",
        "## Forward Mode vs Reverse Mode\n",
        "\n",
        "AD comes in two flavors:\n",
        "\n",
        "**Forward mode** propagates derivatives forward through the computation. For a function $f: \\mathbb{R}^n \\to \\mathbb{R}^m$, computing the full Jacobian requires $n$ forward passes. This is efficient when $n \\ll m$.\n",
        "\n",
        "**Reverse mode** propagates derivatives backward (like backpropagation in neural networks). Computing the full Jacobian requires $m$ reverse passes. This is efficient when $m \\ll n$.\n",
        "\n",
        "For most estimation problems, we have a scalar objective ($m = 1$) and many parameters ($n$ large), so reverse mode is typically preferred. *In my experience however*, I have had more success writing code that is compatible with forward differencing. You will learn from experience that these tools can be fussy.\n",
        "\n",
        "## AD in Julia\n",
        "\n",
        "Julia's AD ecosystem is excellent. The main packages are:\n",
        "\n",
        "### ForwardDiff.jl\n",
        "\n",
        "Forward-mode AD. Simple and robust, works out-of-the-box for most pure Julia code.\n"
      ],
      "id": "b80de840"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using ForwardDiff\n",
        "\n",
        "f(x) = sum(x.^2)\n",
        "x = [1.0, 2.0, 3.0]\n",
        "\n",
        "# Gradient\n",
        "ForwardDiff.gradient(f, x)"
      ],
      "id": "2f1808c3",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "# Hessian\n",
        "ForwardDiff.hessian(f, x)"
      ],
      "id": "3f0946a4",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "### Enzyme.jl\n",
        "\n",
        "A high-performance AD engine that works at the LLVM level. Supports both forward and reverse mode. Often the fastest option, especially for code with loops and mutations.\n"
      ],
      "id": "7d8ca99f"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Enzyme\n",
        "\n",
        "f(x) = x[1]^2 + sin(x[2])\n",
        "\n",
        "x = [1.0, 2.0]\n",
        "dx = zeros(2)\n",
        "\n",
        "# Reverse mode gradient\n",
        "Enzyme.autodiff(Reverse, f, Active, Duplicated(x, dx))\n",
        "dx"
      ],
      "id": "457f3772",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "### Zygote.jl\n",
        "\n",
        "A source-to-source reverse-mode AD system. Popular in machine learning (used by Flux.jl). Works well for array-heavy code but may struggle with control flow.\n"
      ],
      "id": "f10da154"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Zygote\n",
        "\n",
        "f(x) = sum(x.^2)\n",
        "x = [1.0, 2.0, 3.0]\n",
        "\n",
        "Zygote.gradient(f, x)"
      ],
      "id": "58a09fbb",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Practical Recommendations\n",
        "\n",
        "1. **Start with ForwardDiff** for problems with few parameters (< 100). It's the most reliable.\n",
        "\n",
        "2. **Use Enzyme for performance-critical code**, especially if you have loops or in-place mutations.\n",
        "\n",
        "3. **Be aware of limitations**: AD systems can fail on code that uses certain constructs (try-catch, foreign function calls, some global variables). When in doubt, test that your gradients match finite differences:\n"
      ],
      "id": "02c773de"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using ForwardDiff, FiniteDiff\n",
        "\n",
        "f(x) = log(1 + exp(x[1] * x[2])) + x[3]^2\n",
        "x = [1.0, 2.0, 3.0]\n",
        "\n",
        "ad_grad = ForwardDiff.gradient(f, x)\n",
        "fd_grad = FiniteDiff.finite_difference_gradient(f, x)\n",
        "\n",
        "maximum(abs.(ad_grad .- fd_grad))"
      ],
      "id": "07a903f9",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Integration with Optimization\n",
        "\n",
        "Most Julia optimization packages accept AD gradients. Here's an example with `Optim.jl`:\n"
      ],
      "id": "de1414d3"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Optim, ForwardDiff\n",
        "\n",
        "rosenbrock(x) = (1 - x[1])^2 + 100*(x[2] - x[1]^2)^2\n",
        "x0 = [0.0, 0.0]\n",
        "\n",
        "# With automatic gradients via ForwardDiff\n",
        "result = optimize(rosenbrock, x0, LBFGS(); autodiff = :forward)\n",
        "result.minimizer"
      ],
      "id": "67e3a1c2",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Example: Maximum Likelihood with `Optim.jl`\n",
        "\n",
        "Consider a simple probit model:\n",
        "\n",
        "$$ D = \\mathbf{1}\\{X\\beta - \\nu \\geq 0\\},\\qquad \\nu \\sim \\mathcal{N}(0,1) $$\n",
        "\n",
        "Here is code to simulate data for this model:\n"
      ],
      "id": "8a8be767"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Random, Distributions\n",
        "\n",
        "function sim_data(X ; γ)\n",
        "    N = size(X,1)\n",
        "    ν = rand(Normal(),N)\n",
        "    D = (X * γ .- ν) .> 0\n",
        "    return D\n",
        "end\n",
        "\n",
        "# a quick test of the function:\n",
        "N = 1000\n",
        "X = [ones(N) 2*rand(Normal(),N)]\n",
        "γ = [0.1, 0.5]\n",
        "D = sim_data(X ; γ);"
      ],
      "id": "b4aaea4b",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "Consider the problem of estimating $\\gamma$ using maximum likelihood. We will establish the properties of this estimator in class. Here let's just focus on numerically how to attack the minimization problem. The log-likelihood of the data `D` given `X` is:\n",
        "\n",
        "$$ \\mathcal{L}(\\gamma) = \\sum_{n}l(D_{n}; X_{n},\\gamma) = \\sum_{n=1}^{N}D_{n}\\log(\\Phi(X\\gamma)) + (1-D_{n})\\log(1-\\Phi(X\\gamma)) $$\n",
        "\n",
        "Let's write up this likelihood function.\n"
      ],
      "id": "b9bf0a11"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "function log_likelihood(D,X,γ)\n",
        "    ll = 0.\n",
        "    for n in eachindex(D)\n",
        "        xg = X[n,1] * γ[1] + X[n,2] * γ[2] \n",
        "        if D[n]\n",
        "            ll += log(cdf(Normal(),xg))\n",
        "        else\n",
        "            ll += log(1-cdf(Normal(),xg))\n",
        "        end\n",
        "    end\n",
        "    return ll\n",
        "end\n",
        "log_likelihood(D,X,[0.,0.])"
      ],
      "id": "b4fc8b89",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "### Numerical Optimization\n",
        "\n",
        "Optimization is most efficient when we have access to the first and second order derivatives of the function. There is a general class of hill-climbing (or descent in the case of minimization) algorithms that find new guesses $\\gamma_{k+1}$ given $\\gamma_{k}$ as:\n",
        "\n",
        "$$ \\gamma_{k+1} = \\gamma_{k} + \\lambda_{k}A_{k}\\frac{\\partial Q}{\\partial \\gamma} $$\n",
        "\n",
        "where $Q$ is the function being maximized (or minimized). $A_{k}$ defines a direction in which to search (providing weights on the derivatives) and $\\lambda_{k}$ is a scalar variable known as a step-size which is often calculated optimally in each iteration $k$. For Newton's method, the matrix $A_{k}$ is the inverse of the Hessian of the objective function $Q$. Since the hessian can sometimes be expensive to calculate, other methods use approximations to the Hessian that are cheaper to compute.\n",
        "\n",
        "Since we have a simple model, we can calculate derivatives relatively easily. Below we'll compare a hard-coded derivative to this automatic differentiation.\n"
      ],
      "id": "eafd032b"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using ForwardDiff\n",
        "\n",
        "function deriv_ll(D,X,γ)\n",
        "    dll = zeros(2)\n",
        "    for n in eachindex(D)\n",
        "        xg = X[n,1] * γ[1] + X[n,2] * γ[2] \n",
        "        if D[n]\n",
        "            dl = pdf(Normal(),xg) / cdf(Normal(),xg)\n",
        "        else\n",
        "            dl = - pdf(Normal(),xg) / (1 - cdf(Normal(),xg))\n",
        "        end\n",
        "        dll[1] += X[n,1] * dl\n",
        "        dll[2] += X[n,2] * dl            \n",
        "    end\n",
        "    return dll\n",
        "end\n",
        "dx = zeros(2)\n",
        "# forward mode\n",
        "auto_deriv_ll(D,X,γ) = ForwardDiff.gradient(x->log_likelihood(D,X,x),γ)\n",
        "# reverse mode\n",
        "auto_deriv2_ll(D,X,γ,dx) = Enzyme.autodiff(Reverse, x->log_likelihood(D,X,x), Active, Duplicated(γ, dx))\n",
        "\n",
        "d1 = deriv_ll(D,X,γ)\n",
        "d2 = auto_deriv_ll(D,X,γ)\n",
        "auto_deriv2_ll(D,X,γ,dx)\n",
        "[d1 d2 dx]"
      ],
      "id": "9326b30a",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "Ok so we're confident that these functions work as intended, but how do they compare in performance?\n"
      ],
      "id": "3f781c74"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "@time deriv_ll(D,X,γ);\n",
        "@time auto_deriv_ll(D,X,γ);\n",
        "@time auto_deriv2_ll(D,X,γ,dx);"
      ],
      "id": "8d0e3a78",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "All are quite quick and you can see that we're not losing much with automatic differentiation. In my experience, the gap between the two methods can narrow for more complicated functions.\n",
        "\n",
        "So now let's try implementing the maximum likelihood estimator using two different gradient-based algorithms: Newton's Method (which uses the Hessian), and the Broyden–Fletcher–Goldfarb–Shannon (BFGS) algorithm (which updates search direction using changes in the first derivative). \n",
        "\n",
        "While Newton's method requires calculation of the Hessian (second derivatives), BFGS and related methods only require first derivatives. Typically, this makes each iteration quicker but will take more time to converge. Let's test them.\n"
      ],
      "id": "fc513370"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Optim\n",
        "min_objective(x) = -log_likelihood(D,X,x) #<- Optim assumes that we will minimize a function, hence the negative\n",
        "γ_guess = zeros(2)\n",
        "println(\" ---- Using Newton's Method ------ \")\n",
        "res1 = optimize(min_objective,γ_guess,Newton(),autodiff=:forward,Optim.Options(show_trace=true))\n",
        "println(\" ---- Using BFGS ------ \")\n",
        "res2 = optimize(min_objective,γ_guess,BFGS(),autodiff=:forward,Optim.Options(show_trace=true))\n",
        "[res1.minimizer res2.minimizer γ]"
      ],
      "id": "46cda8af",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Further Reading\n",
        "\n",
        "- [JuliaDiff documentation](https://juliadiff.org/)\n",
        "- [ForwardDiff.jl docs](https://juliadiff.org/ForwardDiff.jl/stable/)\n",
        "- [Enzyme.jl docs](https://enzyme.mit.edu/julia/)"
      ],
      "id": "5433a132"
    }
  ],
  "metadata": {
    "kernelspec": {
      "name": "julia-1.11",
      "language": "julia",
      "display_name": "Julia 1.11"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 5
}